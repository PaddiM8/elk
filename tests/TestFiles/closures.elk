fn sel(container) => closure {
    let values = []
    for value in container: values | push(closure(value))

    values
}

let ending = "!"
let result = ["this", "is", "a", "test"]
    | sel => x: x + ending
    | sel => x: x + ending
assertEqual(result[0], "this!!")
assertEqual(result[3], "test!!")

let ending = "!"
let result = ["this", "is", "a", "test"]
    | sel => &op::add(ending)
    | sel => &op::add(ending)
assertEqual(result[0], "this!!")
assertEqual(result[3], "test!!")

let ints = ["1", "2"] | sel => &into::int
assertEqual(ints[0], 1)
assertEqual(ints[1], 2)

let modified = false
fn thing() => closure {
    closure()
}

thing =>: {
    modified = true
}

assert(modified)

# independent closure
let f = &Fn => x: x + "!"
let result = f | call hi
assertEqual(result, "hi!")

# nested closures
fn level1() => closure: closure("1")
fn level2() => closure: closure("2")
fn level3() => closure: closure("3")

level1 => a: {
    level2 => b: {
        level3 => c: assertEqual(a + b + c, "123")
    }
}

# local variable (should take precedence)
let duplicate = 1
[1] | map => x {
    let duplicate = 3
    assertEqual(duplicate, 3)
}

let duplicate2 = 1
[3] | map => duplicate2 {
    assertEqual(duplicate2, 3)
}

# function reference
fn a(x): x + 1
assertEqual(([1, 2, 3] | map => &a | join), "234")
assertEqual(([1, 2, 3] | iter::reduce => &op::mul), 6)

let addFn = &op::add
assertEqual(call(addFn, 2, 3), 5)

fn b(x, y): x + y
assertEqual(([1, 2, 3] | map => &b(10) | join ", "), "11, 12, 13")
assertEqual(([1, 2, 3] | map => &op::mul(3) | math::sum), 18)

fn concat(values...) {
    values | join " "
}

let greeter = &concat("Hello")
greeter | call("world") | assertEqual("Hello world")

let greeter = &op::add("Hello ")
greeter | call("world") | assertEqual("Hello world")

let greeter = &echo("Hello")
greeter | call("world") | assertEqual("Hello world")

# outside variable
let (a, x, b) = (1, 5, 2)
let f = &Fn => { x += 1 }
f | call | assertEqual(6)
x | assertEqual(6)
f | call | assertEqual(7)
assertEqual(a, 1)
assertEqual(b, 2)

# `closure()` inside a closure
fn nestedClosure() => closure {
    &Fn => x: closure(x) * 2
}

let f = nestedClosure => x: x + 1
assertEqual(call(f, 3), 8)

# nested with captured variable
fn nested_with_captured_variable(captured) {
    [[1, 2, 3], [4, 5, 6]]
        | map => arr {
            arr | map => x {
                x + captured
            }
        }
}

nested_with_captured_variable(2)
    | iter::flatten
    | join ", "
    | assertEqual "3, 4, 5, 6, 7, 8"

# return
[1, 2, 3]
    | map => x {
        if x == 1: return 1
        return x + 2
    }
    | join ", "
    | assertEqual "1, 4, 5"

# lazy
[
    [1, 2, 3],
    [4, 5, 6],
    [7, 8, 9],
]
    | map => x { x | map => y: y + 1 }
    | map => &join ", "
    | join ", "
    | assertEqual("2, 3, 4, 5, 6, 7, 8, 9, 10")