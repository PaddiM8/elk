#!/usr/bin/env elk

let sections = "${scriptPath}/input.txt"
    | read
    | str::trim
    | split "\n\n"
let seeds = sections
    | iter::first
    | re::findAll '\d+'
    | map => &into::int
    | iter::collect
let categories = sections
    | iter::skip 1
    | map => &split "\n"
    | map => category {
        category
            | iter:: skip 1
            | map => line { 
                line | split " " | map => &into::int | iter::collect
            }
    }
    | iter::collect

let min = nil
for seed in seeds {
    let current = seed
    for category in categories {
        let range = category | iter::firstOfOrNil => range {
            current in range[1]..=(range[1] + range[2])
        }
        current = range[0] + current - range[1] if range
    }

    min = current if min == nil or current < min
}

min | assertEqual(35)
