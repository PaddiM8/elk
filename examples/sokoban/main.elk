with ./sokoban

fn getBoardString(board) {
    let boardString = ""
    let (_, maxY) = sokoban::getBoardMaxBounds(board)
    for y in 0..(maxY + 1):
        boardString += rowToString(board, y) + "\n"

    boardString
}

fn rowToString(board, y) {
    let row = ""
    let (maxX, _) = sokoban::getBoardMaxBounds(board)
    for x in 0..(maxX + 1) {
        let block = sokoban::getBlock(board, x, y)
        if not block {
            row += " "
        } else {
            let blockType = sokoban::getBlockType(block)
            if blockType == "wall" {
                row += "#"
            } else if blockType == "crate" {
                row += if sokoban::blockIsOnStorage(board, block): "*" else "o"
            } else if blockType == "player" {
                row += if sokoban::blockIsOnStorage(board, block): "+" else "@"
            } else if blockType == "storage" {
                row += "."
            }
        }
    }

    row
}

fn loadBoard(filePath) {
    let board = sokoban::createBoard()
    let fileContent = read(filePath)
    let x = 0
    let y = 0
    for c in fileContent {
        if c == "#" {
            sokoban::addWall(board, x, y)
        } else if c == "@" {
            sokoban::addPlayer(board, x, y)
        } else if c == "+" {
            sokoban::addPlayer(board, x, y)
            sokoban::addStorage(board, x, y)
        } else if c == "o" {
            sokoban::addCrate(board, x, y)
        } else if c == "*" {
            sokoban::addStoredCrate(board, x, y)
        } else if c == "." {
            sokoban::addStorage(board, x, y)
        }

        if c == "\n" {
            x = 0
            y += 1
        } else {
            x += 1
        }
    }

    board
}

fn startGame(mapPath) {
    let board = loadBoard(mapPath)
    let gameDone = false
    while not gameDone {
        updateScreen(board)
        let inputKey = io::getKey()
        if (inputKey == "w") {
            sokoban::movePlayer(board, "up")
        } else if inputKey == "a" {
            sokoban::movePlayer(board, "left")
        } else if inputKey == "s" {
            sokoban::movePlayer(board, "down")
        } else if inputKey == "d" {
            sokoban::movePlayer(board, "right")
        }

        gameDone = inputKey == "q" or sokoban::hasWon(board)
    }

    if sokoban::hasWon(board) {
        updateScreen(board)
        println("You won!")
        println("Press enter to continue")
        input()
        clear()
    }
}

fn updateScreen(board) {
    let boardString = getBoardString(board)
    print("\x1b[?25l")
    clear()
    println("Points: {sokoban::getPoints(board)}")
    println(boardString)
    print("\x1b[?25h")
}

let runGame = true
while runGame {
    println("Welcome to Sokoban, please choose a level:")

    # get levels
    let maps = read(scriptPath() + "/maps.txt") | lines

    for (mapPath, i) in maps | withIndex {
        let mapName = (mapPath | split("."))[0]
        println("{i + 1}. {mapName}")
    }

    let quitId = str(len(maps) + 1)
    println("{quitId}. Quit")

    let mapId = input("Choose: ")
    runGame = mapId != quitId

    if runGame and str::isDigit(mapId) {
        let mapPath = scriptPath() + "/" + maps[int(mapId) - 1]
        startGame(mapPath)
    }
}
