fn countVisible(lines) {
    # Trees by edges are visible, start with those
    let count = (len(lines) - 2) * 2 + len(lines | iter::first) * 2

    # Ignore the trees by the edges since those are always visible
    for y in 1..(len(lines) - 1) {
        for x in 1..(len(lines[y]) - 1) {
            count += 1 if lines | isVisible(x, y)
        }
    }

    count
}

fn mapCondition(items, value):
    items | map => x: x < value
    
fn isVisible(lines, x, y) {
    let value = lines[y][x]

    (lines | iter::at(y)     | iter::take(x)     | mapCondition(value) | iter::allAndAny) or
    (lines | iter::at(y)     | iter::skip(x + 1) | mapCondition(value) | iter::allAndAny) or
    (lines | list::column(x) | iter::take(y)     | mapCondition(value) | iter::allAndAny) or
    (lines | list::column(x) | iter::skip(y + 1) | mapCondition(value) | iter::allAndAny)
}

# 682
cat(scriptPath() + "/input.txt")
    | countVisible
    | assertEqual(682)


