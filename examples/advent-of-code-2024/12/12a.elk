let grid = cat ${scriptPath}/input.txt | into::list

let regions = []
for (line, y) in grid | withIndex {
    for (c, x) in line | withIndex {
        continue if regions | iter::anyOf => region { (x, y) in region }

        let visited = into::set
        traverseRegion(x, y, c, visited)
        regions | push(visited)
    }
}

regions
    | map => region {
        calculatePerimeter(region) * len(region)
    }
    | math::sum
    | assertEqual(211244)

fn traverseRegion(x, y, name, visited) {
    let coords = (x, y)
    return if not (y in 0..len(grid)) or not (x in 0..len(grid[0]))
    return if grid[y][x] != name
    return if coords in visited

    visited | push(coords)
    traverseRegion(x + 1, y, name, visited)
    traverseRegion(x - 1, y, name, visited)
    traverseRegion(x, y + 1, name, visited)
    traverseRegion(x, y - 1, name, visited)
}

fn calculatePerimeter(region) {
    let count = 0
    for (x, y) in region {
        let name = grid[y][x]
        count += 1 if x == 0 or grid[y][x - 1] != name
        count += 1 if y == 0 or grid[y - 1][x] != name
        count += 1 if x == len(grid[0]) - 1 or grid[y][x + 1] != name
        count += 1 if y == len(grid) - 1 or grid[y + 1][x] != name
    }

    count
}
